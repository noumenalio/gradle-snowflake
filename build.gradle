plugins {
   id "com.github.ben-manes.versions" version "0.42.0"
   id "pl.allegro.tech.build.axion-release" version "1.14.0"
   id "com.github.breadmoirai.github-release" version "2.4.1"
   id "build-dashboard"
}

subprojects {
   group = 'io.noumenal'

   repositories {
      mavenCentral()
      maven {
         name 'plugins'
         url "https://plugins.gradle.org/m2/"
      }
   }
}

scmVersion {
   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = true
   localOnly = true
   useHighestVersion = true
}

allprojects {
   project.version = scmVersion.version
}

configurations {
   assets {
      canBeConsumed = false
      canBeResolved = true
   }
}
dependencies {
   subprojects {
      assets(it)
   }
}

githubRelease {
   token project.findProperty('githubToken').toString()
   owner 'noumenalio'
   repo rootProject.name
   overwrite true
   releaseAssets configurations.assets
   generateReleaseNotes true
   //dryRun true
}
