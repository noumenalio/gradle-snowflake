/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package io.noumenal

import groovy.util.logging.Slf4j
import spock.lang.Shared
import spock.lang.Specification
import spock.lang.TempDir
import org.gradle.testkit.runner.GradleRunner

/**
 * A simple functional test for the 'io.noumenal.gradle.snowflake' plugin.
 */
@Slf4j
class FunctionalTest extends Specification {
    @Shared
    def result

    @Shared
    String taskName

    @TempDir
    @Shared
    private File projectDir

    @Shared
    File buildFile, settingsFile, javaFile

    @Shared
    String account = System.getProperty("snowflake.account"),
           user = System.getProperty("snowflake.user"),
           password = System.getProperty("snowflake.password"),
           publishUrl = System.getProperty("snowflake.publishUrl"),
           role = System.getProperty("snowflake.role"),
           database = System.getProperty("snowflake.database"),
           schema = System.getProperty("snowflake.schema"),
           internalStage = System.getProperty("internalStage"),
           externalStage = System.getProperty("externalStage")

    def setupSpec() {
        settingsFile = new File(projectDir, 'settings.gradle')
        settingsFile.write("""
                     |rootProject.name = 'unit-test'
                     |""".stripMargin())

        buildFile = new File(projectDir, 'build.gradle')
        buildFile.write("""
                    |plugins {
                    |    id 'io.noumenal.gradle.snowflake'
                    |    id 'java'
                    |}
                    |java {
                    |    toolchain {
                    |        languageVersion = JavaLanguageVersion.of(11)
                    |    }
                    |}
                    |snowflake {
                    |  groupId = 'io.noumenal'
                    |  artifactId = 'test-gradle-snowflake'
                    |  role = '$role'
                    |  database = '$database'
                    |  schema = '$schema'
                    |  applications {
                    |      add_numbers {
                    |         inputs = ["a integer", "b integer"]
                    |         returns = "string"
                    |         handler = "AddNumbers.addNum"
                    |      }
                    |   }
                    |}
                    |version='0.1.0'
                    |""".stripMargin())

        javaFile = new File("${projectDir}/src/main/java", 'AddNumbers.java')
        javaFile.parentFile.mkdirs()
        javaFile.write("""
                  |public class AddNumbers
                  |{
                  |  public String addNum(int num1, int num2) {
                  |    try {
                  |      int sum = num1 + num2;
                  |      return ("Sum is: " + sum);
                  |    } catch (Exception e) {
                  |      return e.toString();
                  |      }
                  |    }
                  |
                  |    public static void main(String[] args){
                  |      System.out.println("Hello World");
                  |  }
                  |}
                  |""".stripMargin())
    }

    // helper method
    def executeSingleTask(String taskName, List args, Boolean logOutput = true) {
        // ultra secure handling
        List systemArgs = [
                "-Psnowflake.account=$account".toString(),
                "-Psnowflake.user=$user".toString(),
                "-Psnowflake.password=$password".toString()
        ]
        args.add(0, taskName)
        args.addAll(systemArgs)

        // Don't print the password
        //log.warn "runner arguments: ${args}"

        // execute the Gradle test build
        result = GradleRunner.create()
                .withProjectDir(projectDir)
                .withArguments(args)
                .withPluginClasspath()
                .forwardOutput()
                .build()

        // log the results
        if (logOutput) log.warn result.getOutput()
        return result
    }

    def "dry run without publishUrl"() {
        given:
        taskName = 'snowflakePublish'

        when:
        result = executeSingleTask(taskName, ['-Sim'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }

    def "tasks with publishUrl"() {
        given:
        taskName = 'tasks'

        when:
        result = executeSingleTask(taskName, ["-Psnowflake.stage=$externalStage".toString(), "-Psnowflake.publishUrl=$publishUrl".toString(), '-Si'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }

    def "shadowJar"() {
        given:
        taskName = 'shadowJar'

        when:
        result = executeSingleTask(taskName, ['-Si'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }

    def "snowflakePublish with publishUrl option"() {
        given:
        taskName = 'snowflakePublish'

        when:
        result = executeSingleTask(taskName, ["-Psnowflake.publishUrl=${publishUrl}".toString(), "-Psnowflake.stage=$externalStage".toString(), '-Si'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }

    def "snowflakePublish without publishUrl option"() {
        given:
        taskName = 'snowflakePublish'

        when:
        result = executeSingleTask(taskName, ["-Psnowflake.stage=$internalStage".toString(), '-Si'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }

    def "snowflakePublish with custom JAR"() {
        given:
        taskName = 'snowflakePublish'

        when:
        result = executeSingleTask(taskName, ['--jar', 'build/libs/unit-test-0.1.0-all.jar', '-Psnowflake.stage=upload', '-Si'])

        then:
        !result.tasks.collect { it.outcome }.contains('FAILURE')
    }
}
